
################################################################################
# INCLUDE Packages
################################################################################

defineTest( qt ) {
  QT += opengl network script sql
  export(QT)
}

defineTest( glew ) {
  INCLUDEPATH *= /sw/include/GL
  export(INCLUDEPATH)
  LIBS *= -L/sw/lib -lGLEW
  export(LIBS)
}

defineTest( glut ) {
  INCLUDEPATH *= /System/Library/Frameworks/GLUT.framework/Headers
  export(INCLUDEPATH)
  LIBS *= -framework GLUT
  export(LIBS)
}

defineTest( gmm ) {
  INCLUDEPATH += /usr/local/include/gmm
  export(INCLUDEPATH)
}

defineTest( qwt ) {
  INCLUDEPATH *= /usr/local/qwt-5.1.1/include
  export(INCLUDEPATH)
  LIBS *= -L/usr/local/qwt-5.1.1/lib -lqwt
  export(LIBS)
}

defineTest( lapack ) {
  INCLUDEPATH *= /sw/include/lapack
  export(INCLUDEPATH)
  LIBS *= -L/sw/lib/ -llapack
  export(LIBS)
}

defineTest( freetype ) {
  INCLUDEPATH *= /usr/local/include/freetype2
  export(INCLUDEPATH)
}

defineTest( ftgl ) {
  freetype()
  INCLUDEPATH *= /usr/local/include 
  LIBS *= -lftgl
  export(INCLUDEPATH)
  export(LIBS)
}

defineTest( acg ) {
  QMAKE_LIBDIR += $${TOPDIR}/ACG/lib/$${BUILDDIRECTORY}
  LIBS += -lACG
  export(QMAKE_LIBDIR)
  export(LIBS)
}

defineTest( openmesh ) {
  QMAKE_LIBDIR += $${TOPDIR}/OpenMesh/Core/lib/$${BUILDDIRECTORY}
  LIBS += -lCore
  export(QMAKE_LIBDIR)
  export(LIBS)
}
