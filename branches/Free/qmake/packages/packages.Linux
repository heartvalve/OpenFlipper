################################################################################
# INCLUDE Packages
################################################################################

# ACG base directory (many libraries can be found there)
# if ACG is not installed, change it to "/usr"
contains( ARCHITECTURE, 32 ) {
  BASE_PATH=/ACG/acgdev/gcc-4.3-i686
  export(BASE_PATH)
  LIB_BASE=/usr/lib
}
contains( ARCHITECTURE, 64 ) {
  BASE_PATH=/ACG/acgdev/gcc-4.3-x86_64
  export(BASE_PATH)
  LIB_BASE=/usr/lib64 /usr/lib
}

DEBUGADDLIB = true

################################################################################
# Library paths
################################################################################
# Enter correct paths to libraries and header files.
#
# Setting "EXAMPLE_LIB_PATH = /usr/example" for example
# supposes to find header files in /usr/example/include
# and library files in /usr/example/lib
#################################################################################

# gmm (where $${BASE_PATH} is the installation directory of ACG)
GMM_PATH_INC 		= $${BASE_PATH}/gmm-3/include

# superlu
SLU_PATH_INC 		= /usr/include/superlu
SLU_PATH_LIB 		= /usr/lib

# taucs
TAUCS_PATH_INC	= /ACG/acgdev/gcc-4.3-x86_64/taucs-2.2-gfortran/include
TAUCS_PATH_LIB	= /ACG/acgdev/gcc-4.3-x86_64/taucs-2.2.new/lib

# CGAL
CGAL_PATH_INC		= /usr/include/CGAL/
CGAL_PATH_LIB		= /usr/lib

# lapack
LAP_PATH_INC		= /usr/include
LAP_PATH_LIB		= /usr/lib

# wordnet
WN_PATH_INC			= /usr/include
WN_PATH_LIB			= /usr/lib

# qwt
QWT_PATH_INC		= /usr/include/qwt-qt4
QWT_PATH_LIB		= /usr/lib

# glew
GLEW_PATH_INC		= /usr/include/GL
GLEW_PATH_LIB		= /usr/lib

# glut
GLUT_PATH_INC		= /usr/include/GL
GLUT_PATH_LIB		= /usr/lib

# openmp
OMP_PATH_LIB		= /usr/lib/gcc/x86_64-linux-gnu/4.3

# ltilib
LTI_PATH_INC		= $${BASE_PATH}/ltilib/include
LTI_PATH_LIB		= $${BASE_PATH}/ltilib/lib

# fftw
FFTW_PATH_INC		= /usr/include
FFTW_PATH_LIB		= /usr/lib

# freetype2
FT2_PATH_INC		= /usr/include/freetype2
FT2_PATH_LIB		= /usr/lib

# ftgl
FTGL_PATH_INC		= /usr/include/FTGL
FTGL_PATH_LIB		= /usr/lib

# png
PNG_PATH_INC		= /usr/include/libpng
PNG_PATH_LIB		= /usr/lib

# netpbm
NPBM_PATH_INC		= /usr/include
NPBM_PATH_LIB		= /usr/lib

# dc1394
DC1394_PATH_INC	= $${BASE_PATH}/libdc1394-2.0.1/include
DC1394_PATH_LIB	= $${BASE_PATH}/libdc1394-2.0.1/lib

# swissranger
SR_PATH_INC			= $${BASE_PATH}/swissranger/include
SR_PATH_LIB			= $${BASE_PATH}/swissranger/lib

# usb
USB_PATH_INC		= /usr/include
USB_PATH_LIB		= /usr/lib

# cholmod
CHM_PATH_INC		= $${BASE_PATH}/cholmod-1.7.0/include
CHM_PATH_LIB		= $${BASE_PATH}/cholmod-1.7.0/lib

################################################################################

################################################################################
# Don't make any changes below!
################################################################################

defineTest( qt ) {
  CONFIG *= uitools
  export(CONFIG)
  QT += opengl network script sql
  export(QT)
}

defineTest( gmm ) {

	exists ( $${GMM_PATH_INC}/gmm.h ) {

  	INCLUDEPATH += $${GMM_PATH_INC}
  	export(INCLUDEPATH)

		return(true)

	} else {
		message ( WARNING: gmm header files not found! )
	}

	return(false)
}

defineTest( superlu ) {

	exists ( $${SLU_PATH_INC}/slu_Cnames.h ) {
		addLib ( $${LIB_BASE} $${SLU_PATH_LIB}, superlu ) {

			INCLUDEPATH *= $${SLU_PATH_INC}
	  		export(INCLUDEPATH)

			return(true)

		}
	} else {
		message ( WARNING: superlu header files not found! )
	}

	return(false)
}

defineTest( taucs ) {

	exists ( $${TAUCS_PATH_INC}/taucs.h ) {
		exists ( $${TAUCS_PATH_LIB}/libtaucs.a ) {
                        addLib ( $${LIB_BASE} $${TAUCS_PATH_LIB}, metis )

                            lapack()


			    LIBS *= $${TAUCS_PATH_LIB}/libtaucs.a
			    export(LIBS)

    			    INCLUDEPATH *= $${TAUCS_PATH_INC}
			    export(INCLUDEPATH)

			    return(true)

		}
	} else {
		message ( WARNING: taucs header files not found! )
	}

	return(false)
}

defineTest( cgal ) {

	exists ( $${CGAL_PATH_INC} ) {
		addLib ( $${LIB_BASE} $${CGAL_PATH_LIB}, CGAL ) {
			addLib ( $${LIB_BASE} $${CGAL_PATH_LIB}, CGAL_Core ) {
				addLib ( $${LIB_BASE} $${CGAL_PATH_LIB}, CGAL_ImageIO ) {
					addLib ( $${LIB_BASE} $${CGAL_PATH_LIB}, CGAL_PDB ) {
						addLib ( $${LIB_BASE} $${CGAL_PATH_LIB}, CGAL_Qt4 ) {

						        # disabled due to compiler errors with gmp
  						        #INCLUDEPATH *= $${CGAL_PATH_INC}
  						        #export(INCLUDEPATH)
							DEFINES += CGAL_USE_F2C
							export(DEFINES)


							return(true)
						}
					}
				}
			}
		}
	} else {
		message ( WARNING: cgal header files not found! )
	}

	return(false)
}

defineTest( lapack ) {

	exists ( $${LAP_PATH_INC} ) {
		addLib ( $${LIB_BASE} $${LAP_PATH_LIB}, lapack ) {

  		INCLUDEPATH *= $${LAP_PATH_INC}
  		export(INCLUDEPATH)

			return(true)

		}
	} else {
		message ( WARNING: lapack header files not found! )
	}

	return(false)
}

defineTest( wordnet ) {

	exists ( $${WN_PATH_INC}/wn.h ) {
		addLib ( $${LIB_BASE} $${WN_PATH_LIB}, wordnet ) {

  		INCLUDEPATH *= $${WN_PATH_INC}
  		export(INCLUDEPATH)

			return(true)

		}
	} else {
		message ( WARNING: wordnet header files not found! )
	}

	return(false)
}

defineTest( qwt ) {

	exists ( $${QWT_PATH_INC} ) {
		addLib ( $${LIB_BASE} $${QWT_PATH_LIB}, qwt-qt4 ) {

  		INCLUDEPATH *= $${QWT_PATH_INC}
  		export(INCLUDEPATH)

			return(true)

		}
	} else {
		message ( WARNING: qwt header files not found! )
	}

	return(false)
}

defineTest( glew ) {

	exists ( $${GLEW_PATH_INC}/glew.h ) {
		addLib ( $${LIB_BASE} $${GLEW_PATH_LIB}, GLEW ) {

			INCLUDEPATH *= $${GLEW_PATH_INC}
  		export(INCLUDEPATH)

			return(true)

		}
	} else {
		message ( WARNING: glew header files not found! )
	}

	return(false)
}

defineTest( glut ) {

	exists ( $${GLUT_PATH_INC}/glut.h ) {
		addLib ( $${LIB_BASE} $${GLUT_PATH_LIB}, glut ) {

  		INCLUDEPATH *= $${GLUT_PATH_INC}
  		export(INCLUDEPATH)

			return(true)

		}
	} else {
		message ( WARNING: glut header files not found! )
	}

	return(false)
}

defineTest( openmp ) {

  addLib( $${LIB_BASE} $${OMP_PATH_LIB}, gomp ) {

    QMAKE_CXXFLAGS_RELEASE += -fopenmp
    QMAKE_CXXFLAGS_DEBUG   += -fopenmp
    QMAKE_CFLAGS_RELEASE   += -fopenmp
    QMAKE_CFLAGS_DEBUG     += -fopenmp
    QMAKE_LFLAGS_DEBUG     += -fopenmp
    QMAKE_LFLAGS_RELEASE   += -fopenmp

    export(QMAKE_CXXFLAGS_RELEASE)
    export(QMAKE_CFLAGS_RELEASE)
    export(QMAKE_CXXFLAGS_DEBUG)
    export(QMAKE_CFLAGS_DEBUG)
    export(QMAKE_LFLAGS_DEBUG)
    export(QMAKE_LFLAGS_RELEASE)

		return(true)

  } else {
    message("WARNING: Unable to find OpenMP lib for linking. OpenMP support will be disabled!")
  }

	return(false)
}

defineTest( ltilib ) {

	exists ( $${LTI_PATH_INC} ) {
		CONFIG( debug, debug|release ){
			# debug
			TEST = addLib( $${LIB_BASE} $${LTI_PATH_LIB}, ltid )
  	} else {
			# release
  		TEST = addLib( $${LIB_BASE} $${LTI_PATH_LIB}, ltir )
  	}
		TEST {
			addLib( $${LIB_BASE} $${LTI_PATH_LIB}, jpeg ) {

		  	INCLUDEPATH *= $${LTI_PATH_INC}
				export(INCLUDEPATH)

				return(true)

			}
		}
	} else {
		message ( WARNING: ltilib header files not found! )
	}

	return(false)
}

defineTest( fftw ) {

	exists ( $${FFTW_PATH_INC}/fftw.h ) {
		addLib ( $${LIB_BASE} $${FFTW_PATH_LIB}, fftw3 ) {
			addLib ( $${LIB_BASE} $${FFTW_PATH_LIB}, fftw3f ) {

  			INCLUDEPATH *= $${FFTW_PATH_INC}
  			export(INCLUDEPATH)

				return(true)

			}
		}
	} else {
		message ( WARNING: fftw header files not found! )
	}

	return(false)
}

defineTest( freetype ) {

	exists ( $${FT2_PATH_INC} ) {
		addLib ( $${LIB_BASE}  $${FT2_PATH_LIB}, freetype ) {

  		INCLUDEPATH *= $${FT2_PATH_INC}
  		export(INCLUDEPATH)

			return(true)

		}
	} else {
		message ( WARNING: freetype2 header files not found! )
	}

	return(false)
}

defineTest( ftgl ) {

	freetype() {
		exists ( $${FTGL_PATH_INC} ) {
			addLib ( $${LIB_BASE} $${FTGL_PATH_LIB}, ftgl ) {

				INCLUDEPATH *= $${FTGL_PATH_INC}
				export(INCLUDEPATH)
  			DEFINES += USE_FTGL
  			export(DEFINES)

				return(true)

			}
		} else {
		message ( WARNING: ftgl header files not found! )
		}
	}

  return(false)
}


defineTest( png ) {

	exists ( $${PNG_PATH_INC} ) {
		addLib ( $${LIB_BASE} $${PNG_PATH_LIB}, png ) {

			INCLUDEPATH *= $${PNG_PATH_INC}
			export(INCLUDEPATH)

			return(true)

		}
	} else {
		message ( WARNING: png header files not found! )
	}

	return(false)
}


defineTest( netpbm ) {

	exists ( $${NPBM_PATH_INC} ) {
		addLib ( $${LIB_BASE} $${NPBM_PATH_LIB}, netpbm ) {

			INCLUDEPATH *= $${NPBM_PATH_INC}
  		export(INCLUDEPATH)

			return(true)

		}
	} else {
		message ( WARNING: netpbm header files not found! )
	}

	return(false)
}


defineTest( dc1394 ) {

	exists ( $${DC1394_PATH_INC} ) {
		addLib ( $${LIB_BASE} $${DC1394_PATH_LIB}, dc1394 ) {

  		INCLUDEPATH *= $${DC1394_PATH_INC}
  		export(INCLUDEPATH)

			return(true)

		}
	} else {
		message ( WARNING: dc1394 header files not found! )
	}

	return(false)
}


defineTest( swissranger ) {

	exists ( $${SR_PATH_INC} ) {
		addLib ( $${LIB_BASE} $${SR_PATH_LIB}, usbSR ) {

  		INCLUDEPATH *= $${SR_PATH_INC}
  		export(INCLUDEPATH)

			return(true)

		}
	} else {
		message ( WARNING: swissranger header files not found! )
	}

	return(false)
}


defineTest( cuda ) {

  CONFIG(debug, debug|release){
    CUDA_NVCCFLAGS_DEBUG += -g -deviceemu
  } else {

  }


  unix {
    # auto-detect CUDA path
    CUDA_DIR = $$system(which nvcc)
    CUDA_DIR = $$replace(CUDA_DIR,/bin/nvcc,/)
    isEmpty( CUDA_DIR ) {
        exists(/usr/local/cuda) {
            CUDA_DIR = '/usr/local/cuda'
        }
        isEmpty( CUDA_DIR ) {
            CUDA_DIR = '/ACG/acgdev/gcc-4.0-x86_64/cuda2'
        }
        exists($$CUDA_DIR){
            !build_pass : message("Found CUDA at" $$CUDA_DIR)
        }
    }
    NVCCPATH = $$CUDA_DIR/bin/nvcc
    INCLUDEPATH += $$CUDA_DIR/include
    QMAKE_LIBDIR += $$CUDA_DIR/lib
    LIBS += -Wl,-rpath=$$CUDA_DIR/lib -lcudart


    cuda.output = ${OBJECTS_DIR}${QMAKE_FILE_BASE}_cuda.obj
    cuda.commands = $$NVCCPATH $$CUDA_NVCCFLAGS_DEBUG -c -Xcompiler -fPIC -Xcompiler $$join(QMAKE_CXXFLAGS,",") $$join(INCLUDEPATH,'" -I "','-I "','"') ${QMAKE_FILE_NAME} -Xptxas -v -o ${QMAKE_FILE_OUT}
    cuda.dependcy_type = TYPE_C
    cuda.depend_command = $$NVCCPATH $$CUDA_NVCCFLAGS_DEBUG -M -Xcompiler $$join(QMAKE_CXXFLAGS,",") $$join(INCLUDEPATH,'" -I "','-I "','"') ${QMAKE_FILE_NAME} | sed "s/^.*:\ //" | sed "s/^\ *//" | tr -d '\\\n'
  }

  cuda.input = CUDA_SOURCES

  export(INCLUDEPATH)
  export(QMAKE_LIBDIR)
  export(LIBS)
  export(CUDA_DIR)
  export(NVCCPATH)

  export(cuda.output)
  export(cuda.commands)
  export(cuda.dependcy_type)
  export(cuda.depend_command)
  export(cuda.input)

  QMAKE_EXTRA_COMPILERS += cuda
  export(QMAKE_EXTRA_COMPILERS)
}

defineTest( usb ) {

	exists ( $${USB_PATH_INC}/usb.h ) {
		addLib ( $${LIB_BASE} $${USB_PATH_LIB}, usb ) {
			addLib ( $${LIB_BASE} $${USB_PATH_LIB}, usbpp ) {

				INCLUDEPATH *= $${USB_PATH_INC}
  			export(INCLUDEPATH)

				return(true)

			}
		}
	} else {
		message ( WARNING: usb header files not found! )
	}

	return(false)
}



defineTest( cholmod ) {

	exists ( $${CHM_PATH_INC} ) {
		addLib ( $${LIB_BASE} $${CHM_PATH_LIB}, cholmod ) {
			addLib ( $${LIB_BASE} $${CHM_PATH_LIB}, colamd ) {
				addLib ( $${LIB_BASE} $${CHM_PATH_LIB}, ccolamd ) {
					addLib ( $${LIB_BASE} $${CHM_PATH_LIB}, camd ) {
						addLib ( $${LIB_BASE} $${CHM_PATH_LIB}, amd ) {
							addLib ( $${LIB_BASE} $${CHM_PATH_LIB}, metis ) {
								addLib ( $${LIB_BASE} $${CHM_PATH_LIB}, lapack ) {

  								INCLUDEPATH *= $${CHM_PATH_INC}
  								export(INCLUDEPATH)

									return(true)

								}
							}
						}
					}
				}
			}
		}
	} else {
		message ( WARNING: cholmod header files not found! )
	}

	return(false)
}

defineTest( acg ) {
  QMAKE_LIBDIR += $${TOPDIR}/ACG/lib/$${BUILDDIRECTORY}
  INCLUDEPATH += $${TOPDIR}/ACG/include
  LIBS+= -Wl,-rpath=$${TOPDIR}/ACG/lib/$${BUILDDIRECTORY} -lACG
  export(INCLUDEPATH)
  export(QMAKE_LIBDIR)
  export(LIBS)
}




defineTest( openmesh ) {
  INCLUDEPATH += $${TOPDIR}/OpenMesh/src
  DEPENDPATH  += $${TOPDIR}/OpenMesh/src

  QMAKE_LIBDIR += $${TOPDIR}/OpenMesh/lib

  CONFIG( debug, debug|release ){
    LIBS+= -Wl,-rpath=$${TOPDIR}/OpenMesh/lib -lOpenMeshCored
    LIBS+= -Wl,-rpath=$${TOPDIR}/OpenMesh/lib -lOpenMeshToolsd
  } else {
    LIBS+= -Wl,-rpath=$${TOPDIR}/OpenMesh/lib -lOpenMeshCore
    LIBS+= -Wl,-rpath=$${TOPDIR}/OpenMesh/lib -lOpenMeshTools
  }

  export(INCLUDEPATH)
  export(QMAKE_LIBDIR)
  export(LIBS)
}

defineTest( physim2 ) {
  exists( $${TOPDIR}/PhySim ) {
    QMAKE_LIBDIR += $${TOPDIR}/PhySim/lib/$${BUILDDIRECTORY}
    LIBS +=  -Wl,-rpath=$${TOPDIR}/PhySim/lib/$${BUILDDIRECTORY} -lPhySim
    INCLUDEPATH *= $${TOPDIR}/PhySim $${TOPDIR}/PhySim/include
    export(QMAKE_LIBDIR)
    export(LIBS)
    export(INCLUDEPATH)
  }
}

defineTest( petsc ) {
        INCLUDEPATH *= /usr/include/petsc
        export(INCLUDEPATH)
        QMAKE_LIBDIR += /usr/lib
        export(QMAKE_LIBDIR)
        LIBS *= -Wl,-rpath=/usr/lib -lpetsc -lpetscmat -lpetscsnes -lpetscts -lpetscvec -lpetscksp -lpetscdm
        export(LIBS)
}

defineTest( slepc ) {
        INCLUDEPATH *= /usr/include/slepc
        export(INCLUDEPATH)
        QMAKE_LIBDIR += /usr/lib
        export(QMAKE_LIBDIR)
	DEFINES += WITH_SLEPC_3
        LIBS *= -Wl,-rpath=/usr/lib -lslepc
        export(LIBS)
	export DEFINES
}

defineTest( mpi ) {
        INCLUDEPATH *= /usr/lib/openmpi/include
        export(INCLUDEPATH)
        QMAKE_LIBDIR += /usr/lib/openmpi/lib
        export(QMAKE_LIBDIR)
        LIBS *= -Wl,-rpath=/usr/lib/openmpi/lib -lmpi -lmpi_cxx -lmpi_f77
        export(LIBS)
}

defineTest( gsl ) {
        INCLUDEPATH *= /usr/include
        export(INCLUDEPATH)
        LIBS *= -Wl,-rpath=/usr/lib -lgsl
        export(LIBS)
}



